open! Alice_stdlib
open Alice_hierarchy
open Alice_package.Dependency_graph

let basename = Basename.of_filename ".merlin"

let header_comment =
  {|# Generated by Alice.
# This file is necessary for LSP to work in Alice projects.
# It will be automatically updated when the project is built.
# Don't check this file into version control.
# See https://www.alicecaml.org/lsp for more info.
|}
;;

let dot_merlin_text_initial () =
  let source_rel_path = Relative_path.of_basename Alice_package.Package.src in
  sprintf "%s\nS %s" header_comment (Relative_path.to_filename source_rel_path)
;;

let dot_merlin_text package_with_deps build_dir profile ~ocamllib_path =
  let build_abs_paths_for_deps =
    Package_with_deps.immediate_deps_in_dependency_order package_with_deps
    |> List.concat_map ~f:(fun dep ->
      let dep_id = Package_with_deps.id dep in
      [ Build_dir.package_public_dir build_dir dep_id profile
      ; Build_dir.package_public_for_lsp_dir build_dir dep_id profile
      ])
  in
  let build_abs_paths =
    Build_dir.package_private_dir
      build_dir
      (Package_with_deps.id package_with_deps)
      profile
    :: build_abs_paths_for_deps
  in
  let to_relative_paths ~prefix =
    List.map build_abs_paths ~f:(Absolute_path.chop_prefix ~prefix)
  in
  let build_rel_paths =
    match Package_with_deps.package package_with_deps |> Alice_package.Package.root with
    | `Root prefix -> to_relative_paths ~prefix
    | `Non_root prefix -> to_relative_paths ~prefix
  in
  let source_rel_path = Relative_path.of_basename Alice_package.Package.src in
  let ocamllib_path_line =
    (* It's probably more correct to specify the OCaml library path with
       "FLG -ocamllib-path" however this doesn't seem to work on Windows, so
       just use a "B" directive instead. *)
    sprintf "B %s" (Absolute_path.to_filename ocamllib_path)
  in
  let lines =
    sprintf "S %s" (Relative_path.to_filename source_rel_path)
    :: ocamllib_path_line
    :: List.map build_rel_paths ~f:(fun build_rel_path ->
      sprintf "B %s" (Relative_path.to_filename build_rel_path))
  in
  let text = String.concat lines ~sep:"\n" in
  sprintf "%s\n%s" header_comment text
;;
